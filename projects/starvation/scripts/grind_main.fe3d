22 25
META script_type_update
META script_execution_waiting

/// Values
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update wheat grinding
fe3d:text_set_visible("grind", <false>)
STR hoveredID = fe3d:raycast_into_model("mill", "door", <true>)
DEC distance = 0.0
IF hoveredID NOT "" AND _isBuilding IS <false>:
    EDIT distance = fe3d:raycast_into_model_distance("mill", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0
    /// Retrieve mill values
    STR millAabbID = misc:concat_strings(hoveredID, "@main")
    VEC3 millPos = fe3d:model_get_position(hoveredID)
    VEC3 millRot = fe3d:model_get_rotation(hoveredID)
    VEC3 millSize = fe3d:aabb_get_size(millAabbID)
    DEC millHeight = fe3d:model_get_max_height(hoveredID)
    /// Check if mill is fully built & hovered
    BOOL isBuilt = (millHeight IS millSize.y OR millHeight MORE millSize.y)
    IF distance LESS _useReach AND isBuilt IS <true>:
        /// Check if player has enough wheat to grind
        STR flourID = misc:concat_strings("flour:", hoveredID)
        BOOL isFlourExisting = fe3d:model_is_existing(flourID)
        INT minWheat = _wheatCount
        DECR minWheat 1
        IF isFlourExisting IS <false> AND _bag[4] MORE minWheat:
            fe3d:text_set_visible("grind", <true>)
            IF ePressed IS <true>:
                DECR _bag[4] _wheatCount
                fe3d:sound_play_forced("grind", 0, 0)
                VEC3 flourPos = fe3d:model_get_position(hoveredID)
                IF millRot.y IS 0.0:
                    INCR flourPos.x 5.75
                    DECR flourPos.z 1.5
                ELIF millRot.y IS 90.0:
                    DECR flourPos.z 5.75
                    DECR flourPos.x 1.5
                ELIF millRot.y IS 180.0:
                    DECR flourPos.x 5.75
                    INCR flourPos.z 1.5
                ELSE:
                    INCR flourPos.x 1.5
                    INCR flourPos.z 5.75
                fe3d:model_place(flourID, "flour", flourPos.x, flourPos.y, flourPos.z)
                fe3d:model_set_rotation(flourID, millRot.x, millRot.y, millRot.z)

/// Update flour pickup
EDIT hoveredID = fe3d:raycast_into_model("flour", "", <true>)
IF hoveredID NOT "" AND _wasPickupPlaying IS <true> AND _isPickupPlaying IS <false>:
    DEC flourDistance = fe3d:raycast_into_model_distance("flour", "", <true>)
    IF flourDistance IS -1.0:
        EDIT flourDistance = 1000.0
    IF flourDistance LESS _pickupReach:
        INCR _bag[5] _flourCount
        fe3d:model_delete(hoveredID)
        fe3d:sound_play_forced("bag", 0, 0)
