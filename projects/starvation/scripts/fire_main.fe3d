57 20
META script_type_update
META script_execution_waiting

/// Values
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update torch realism
BOOL equipped = (_equipIndex IS 7 AND _inventory[_equipIndex] NOT "")
fe3d:light_set_visible("torch", <false>)
fe3d:billboard_set_visible("torch", <false>)
fe3d:sound_set_volume("torch", 0.0)
IF equipped IS <true> AND _isInvOpen IS <true>:
    /// Set torch position to player position
    VEC3 torchPos = fe3d:model_get_position("torch")
    VEC3 firePos = torchPos
    INCR torchPos.y 2.0
    INCR firePos.y 1.0
    fe3d:light_set_position("torch", torchPos.x, torchPos.y, torchPos.z)
    fe3d:billboard_set_position("torch", firePos.x, firePos.y, firePos.z)
    /// Only light up torch if nighttime
    IF _isDaytime IS <false> AND _isUnderwater IS <false>:
        fe3d:light_set_visible("torch", <true>)
        fe3d:billboard_set_visible("torch", <true>)
        DEC durabilityHit = _durabilityHit
        MUL durabilityHit 0.001
        DECR _durabilities[7] durabilityHit
        fe3d:sound_set_volume("torch", 0.75)

/// Update campfire realism
CONST LIST billboards = fe3d:billboard_find_ids("fire")
LIST lights = fe3d:light_find_ids("campfire")
EDIT lights = misc:concat_lists("lights", "lights")
LIST sounds = fe3d:sound_find_ids("campfire")
EDIT sounds = misc:concat_lists("sounds", "sounds")
VEC3 camPos = fe3d:camera_get_position()
VEC3 billboardPos = [0.0 0.0 0.0]
DEC campfireDistance = 0.0
DEC heatFactor = _temperatureFireFactor
MUL heatFactor 0.5
INT index = 0
INT maxIndex = misc:get_list_size("billboards")
LOOP:
    IF index IS maxIndex:
        BREAK
    ELSE:
        IF _isDaytime IS <true>:
            fe3d:billboard_set_visible(billboards[index], <false>)
            fe3d:light_set_visible(lights[index], <false>)
            fe3d:sound_set_max_volume(sounds[index], 0.0)
        ELSE:
            fe3d:billboard_set_visible(billboards[index], <true>)
            fe3d:light_set_visible(lights[index], <true>)
            fe3d:sound_set_max_volume(sounds[index], 1.0)
            EDIT billboardPos = fe3d:billboard_get_position(billboards[index])
            EDIT campfireDistance = math:distance(camPos, billboardPos)
            IF campfireDistance LESS _maxCampfireHeatDistance:
                INCR _temperature heatFactor
        INCR index 1
