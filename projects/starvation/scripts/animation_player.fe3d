18 56
META script_type_update
META script_execution_waiting

/// Update skeleton position & rotation
VEC3 camPos = fe3d:camera_get_position()
DEC yaw = fe3d:camera_get_yaw()
NEG yaw
INCR yaw 90.0
DECR camPos.y _playerHeight
fe3d:model_set_position("player", camPos.x, camPos.y, camPos.z)
fe3d:model_set_rotation("player", 0.0, yaw, 0.0)

/// Update player walking animation
BOOL isWalkPlaying = fe3d:model_is_animation_started("player", "walk")
IF _isAbleToMove IS <false> AND isWalkPlaying IS <true>:
    fe3d:model_fade_animation("player", "walk", 1)
ELIF _isPickupPlaying IS <false> AND _isAbleToMove IS <true>:
    IF _isMoving IS <true> AND isWalkPlaying IS <false>:
        fe3d:model_start_animation("player", "walk", -1)
    IF isWalkPlaying IS <true>:
        DEC speed = 1.0
        IF _totalBagWeight MORE _maxBagWeight:
            DEC weightFactor = _maxBagWeight
            DIV weightFactor _totalBagWeight
            MUL speed weightFactor
        IF _isUnderwater IS <true>:
            MUL speed 0.5
        IF _isRunning IS <true>:
            MUL speed 2.0
        IF _isMoving IS <false>:
            fe3d:model_fade_animation("player", "walk", 1)
            MUL speed 2.0
        fe3d:model_set_animation_speed("player", "walk", speed)

/// Update pickup animation
BOOL fPressed = fe3d:input_is_key_pressed("KEY_F")
BOOL a = (_isAbleToMove IS <true> AND _isJumping IS <false> AND _isFalling IS <false>)
BOOL b = (isWalkPlaying IS <false> AND _isUnderwater IS <false> AND fPressed IS <true>)
IF a IS <true> AND b IS <true>:
    BOOL alreadyStarted = fe3d:model_is_animation_started("player", "pickup")
    IF alreadyStarted IS <false>:
        fe3d:model_start_animation("player", "pickup", 0)
