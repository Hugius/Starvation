7 39
META script_type_update
META script_execution_waiting

/// Values
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update iron smelting
fe3d:text_set_visible("smelt", <false>)
STR hoveredID = fe3d:raycast_into_model("smelter", "hole", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    /// Retrieve smelter values
    STR smelterAabbID = misc:concat_strings(hoveredID, "@main")
    VEC3 smelterSize = fe3d:aabb_get_size(smelterAabbID)
    DEC smelterHeight = fe3d:model_get_max_height(hoveredID)
    DEC smelterDistance = fe3d:raycast_into_model_distance("smelter", "hole", <true>)
    IF smelterDistance IS -1.0:
        EDIT smelterDistance = 1000.0
    /// Check if smelter is fully built & hovered
    BOOL isBuilt = (smelterHeight IS smelterSize.y OR smelterHeight MORE smelterSize.y)
    IF smelterDistance LESS _useReach AND isBuilt IS <true>:
        STR ingotID = misc:concat_strings("ingot:", hoveredID)
        BOOL isIngotExisting = fe3d:model_is_existing(ingotID)
        /// Check if player has enough ore to smelt
        IF isIngotExisting IS <false> AND _bag[9] MORE 0:
            fe3d:text_set_visible("smelt", <true>)
            IF ePressed IS <true>:
                DECR _bag[9] _oreCount
                fe3d:sound_play_forced("smelt", 0, 0)
                VEC3 pos = fe3d:model_get_position(hoveredID)
                VEC3 rot = fe3d:model_get_rotation(hoveredID)
                IF rot.y IS 0.0:
                    INCR pos.x 1.25
                ELIF rot.y IS 90.0:
                    DECR pos.z 1.25
                ELIF rot.y IS 180.0:
                    DECR pos.x 1.25
                ELSE:
                    INCR pos.z 1.25
                INCR pos.y 1.65
                fe3d:model_place(ingotID, "ingot", pos.x, pos.y, pos.z)
                fe3d:model_set_rotation(ingotID, rot.x, rot.y, rot.z)

/// Update ingot pickup
EDIT hoveredID = fe3d:raycast_into_model("ingot:smelter", "", <true>)
IF hoveredID NOT "" AND _wasPickupPlaying IS <true> AND _isPickupPlaying IS <false>:
    DEC ingotDistance = fe3d:raycast_into_model_distance("ingot:smelter", "", <true>)
    IF ingotDistance IS -1.0:
        EDIT ingotDistance = 1000.0
    IF ingotDistance LESS _pickupReach:
        INCR _bag[10] _ingotCount
        fe3d:model_delete(hoveredID)
        fe3d:sound_play_forced("bag", 0, 0)
