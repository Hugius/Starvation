80 63
META script_type_update
META script_execution_waiting

/// Update health icon color
DEC healthColor = _health
fe3d:image_set_color("health", healthColor, healthColor, healthColor)
/// Update hunger icon color
DEC hungerColor = _hunger
fe3d:image_set_color("hunger", hungerColor, hungerColor, hungerColor)
/// Update thirst icon color
DEC thirstColor = _thirst
fe3d:image_set_color("thirst", thirstColor, thirstColor, thirstColor)
/// Update hygiene icon color
DEC hygieneColor = _hygiene
fe3d:image_set_color("hygiene", hygieneColor, hygieneColor, hygieneColor)
/// Update oxygen icon color
DEC oxygenColor = _oxygen
fe3d:image_set_color("oxygen", oxygenColor, oxygenColor, oxygenColor)
/// Update temperature icon color
DEC tempColor = _temperature
fe3d:image_set_color("temperature", tempColor, tempColor, 1.0)
/// Update stamina icon color
DEC staminaColor = _stamina
fe3d:image_set_color("stamina", staminaColor, staminaColor, staminaColor)
/// Update hunger
DECR _hunger _hungerFactor
EDIT _hunger = math:clamp(_hunger, 0.0, 1.0)

/// Update thirst
DECR _thirst _thirstFactor
EDIT _thirst = math:clamp(_thirst, 0.0, 1.0)

/// Update stamina
EDIT _stamina = math:clamp(_stamina, 0.0, 10.0)

/// Update oxygen
IF _isUnderwater IS <true>:
    DECR _oxygen _oxygenFactor
ELSE:
    INCR _oxygen _oxygenBreathFactor
EDIT _oxygen = math:clamp(_oxygen, 0.0, 1.0)

/// Update temperature
BOOL torchEquipped = (_isInvOpen IS <true> AND _inventory[_equipIndex] IS "torch")
IF _isDaytime IS <true> OR _isSleeping IS <true>:
    INCR _temperature _temperatureDayFactor
IF _isUnderwater IS <true>:
    DECR _temperature _temperatureWaterFactor
IF _isDaytime IS <false> AND _isSleeping IS <false>:
    IF torchEquipped IS <true> AND _isUnderwater IS <false>:
        INCR _temperature _temperatureFireFactor
    ELSE:
        DECR _temperature _temperatureNightFactor
EDIT _temperature = math:clamp(_temperature, 0.0, 1.0)

/// Update hygiene
DECR _hygiene _hygieneFactor
EDIT _hygiene = math:clamp(_hygiene, 0.0, 1.0)

/// Update health
DEC hu = _hunger
DEC th = _thirst
DEC hy = _hygiene
DEC ox = _oxygen
DEC te = _temperature
IF hu IS 0.0 OR th IS 0.0 OR hy IS 0.0 OR ox IS 0.0 OR te IS 0.0:
    DECR _health _healthFactor
ELSE:
    INCR _health _healthRegenFactor
EDIT _health = math:clamp(_health, 0.0, 1.0)

/// Update health sound
DEC volume = 1.0
DECR volume _health
fe3d:sound_set_volume("heart", volume)

/// Check if player died
IF _health IS 0.0 OR _health LESS 0.0:
    fe3d:camera_disable_first_person_view()
    fe3d:image_set_alpha("black", 1.0)
    fe3d:text_place("death", "font.ttf", "Rest In Peace", 0.0, 0.0, 0.0, 0.325, 0.075)
    fe3d:sound_stop_all()
    fe3d:sound_play("death", 0, 0)
    BOOL isExisting = fe3d:scene_is_custom_existing("starvia")
    IF isExisting IS <true>:
        fe3d:scene_delete_custom("starvia")
