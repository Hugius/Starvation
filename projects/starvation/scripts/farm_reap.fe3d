39 13
META script_type_update
META script_execution_waiting

/// Values
STR toolID = _inventory[_equipIndex]
STR wheatAabbID = ""
VEC3 camPos = fe3d:camera_get_position()
VEC3 wheatSize = [0.0 0.0 0.0]
DEC wheatHeight = 0.0

/// Update wheat hover
STR hoveredID = fe3d:raycast_into_model("wheat", "", <true>)
DEC distance = 0.0
IF hoveredID NOT "":
    EDIT distance = fe3d:raycast_into_model_distance("wheat", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0

/// Update wheat reaping
fe3d:text_set_visible("reap", <false>)
IF hoveredID NOT "" AND distance LESS _toolReach:
    EDIT wheatAabbID = misc:concat_strings(hoveredID, "@wheat")
    EDIT wheatHeight = fe3d:model_get_max_height(hoveredID)
    EDIT wheatSize = fe3d:aabb_get_size(wheatAabbID)
    BOOL isFullyGrown = (wheatHeight IS wheatSize.y OR wheatHeight MORE wheatSize.y)
    IF isFullyGrown IS <true> AND toolID IS "scythe" AND _isInvOpen IS <true>:
        fe3d:text_set_visible("reap", <true>)
        IF _wasScythePlaying IS <true> AND _isScythePlaying IS <false>:
            fe3d:model_delete(hoveredID)
            INT seedsCount = _wheatCount
            MUL seedsCount 2
            INCR _bag[3] seedsCount
            INCR _bag[4] _wheatCount
            fe3d:sound_play("bag", 0, 0)

/// Update scythe sound
BOOL isSoundPlaying = fe3d:sound_is_playing("scythe")
BOOL isAllowed = fe3d:text_is_visible("reap")
IF isAllowed IS <true> AND _isScythePlaying IS <true> AND isSoundPlaying IS <false>:
    INT frameIndex = fe3d:model_get_animation_frame_index(toolID, "scythe_reap")
    IF frameIndex IS 4:
        fe3d:sound_play("scythe", 0, 0)
