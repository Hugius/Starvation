36 60
META script_type_update
META script_execution_waiting

/// Values
STR toolID = _inventory[_equipIndex]
VEC3 terrainPoint = fe3d:raycast_get_position_on_terrain()
VEC3 camPos = fe3d:camera_get_position()
DEC waterY = fe3d:water_get_height()
DEC maxCamY = 7.0
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL isValid = fe3d:raycast_is_position_on_terrain_valid()
BOOL allowed = (terrainPoint.y LESS waterY AND isValid IS <true>)

/// Check if throwing is finished
IF _isrodPlaying IS <true>:
    BOOL resumed = fe3d:model_is_animation_playing("rod", "rod_throw")
    INT frameIndex = fe3d:model_get_animation_frame_index("rod", "rod_throw")
    /// Pause rod animation when throwing is done, resume when cancelling
    IF frameIndex IS 3 AND _isFishing IS <false>:
        fe3d:model_pause_animation("rod", "rod_throw")
        IF allowed IS <true> AND camPos.y LESS maxCamY AND camPos.y MORE waterY:
            fe3d:sound_play("rod", 0, 0)
        EDIT _isFishing = <true>
    ELIF lmbPressed IS <true> AND _isFishing IS <true> AND resumed IS <false>:
        fe3d:model_resume_animation("rod", "rod_throw")
ELSE:
    EDIT _isFishing = <false>

/// Update fish catching process
fe3d:text_set_visible("fish", <false>)
/// Check if allowed to fish
IF allowed IS <true> AND camPos.y LESS maxCamY AND camPos.y MORE waterY:
    IF _isFishing IS <true>:
        BOOL isPlaying = fe3d:model_is_animation_playing("rod", "rod_throw")
        IF isPlaying IS <false>:
            /// A small chance for the fish to bite
            INT randomInt = misc:get_random_integer(0, _fishChance)
            IF randomInt IS 0:
                /// Fishing done, add fish to inventory
                fe3d:model_resume_animation("rod", "rod_throw")
                INCR _bag[7] _fishCount
                DECR _durabilities[8] _durabilityHit
                fe3d:sound_play("fish", 0, 0)
                fe3d:sound_play("bag", 0, 0)
    ELIF toolID IS "rod" AND _isInvOpen IS <true> AND _isrodPlaying IS <false>:
        fe3d:text_set_visible("fish", <true>)
