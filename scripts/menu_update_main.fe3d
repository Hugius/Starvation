29 29
META script_type_update
META script_execution_waiting

/// Values
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")

/// Handle buttons click
IF lmbPressed IS <true>:
    /// Determine which button was clicked
    BOOL newGame = (_hoveredMenuButtonID IS "menu_newgame")
    BOOL loadGame = (_hoveredMenuButtonID IS "menu_loadgame")
    IF newGame IS <true> OR loadGame IS <true>:
        IF newGame IS <true>:
            fe3d:scene_load("starvia")
        ELSE:
            fe3d:scene_load_custom("starvia")
        fe3d:image_place("vignette", "vignette.png", 0.0, 0.0, 0.0, 2.0, 2.0)
        EXEC npc_init
        EXEC time_init
        EXEC water_init
        EXEC build_init_main
        EXEC build_init_gui
        EXEC craft_init_main
        EXEC craft_init_gui
        EXEC storage_init_main
        EXEC storage_init_gui
        EXEC inventory_init_main
        EXEC inventory_init_gui
        EXEC bag_init
        EXEC player_init_vars
        EXEC player_init_main
        EXEC animal_init
        EXEC misc_init_vars
        EXEC misc_init_main
        IF loadGame IS <true>:
            EXEC load_player
            EXEC load_misc
        /// Close menu
        EDIT _isInMenu = <false>
        fe3d:image_set_visible("menu_background", <false>)
        fe3d:image_set_visible("menu_title", <false>)
        fe3d:image_set_visible("menu_newgame", <false>)
        fe3d:image_set_visible("menu_loadgame", <false>)
        fe3d:image_set_visible("menu_graphics", <false>)
        fe3d:image_set_visible("menu_sound", <false>)
        fe3d:image_set_visible("menu_music", <false>)
        fe3d:image_set_visible("menu_quit", <false>)
        fe3d:text_set_visible("menu_newgame", <false>)
        fe3d:text_set_visible("menu_loadgame", <false>)
        fe3d:text_set_visible("menu_graphics", <false>)
        fe3d:text_set_visible("menu_sound", <false>)
        fe3d:text_set_visible("menu_music", <false>)
        fe3d:text_set_visible("menu_quit", <false>)
    ELIF _hoveredMenuButtonID IS "menu_graphics":
        IF _graphicsQuality IS "low":
            EDIT _graphicsQuality = "medium"
            fe3d:text_set_content("menu_graphics", "GFX:medium")
            fe3d:text_set_size("menu_graphics", 0.4375, 0.075)
        ELIF _graphicsQuality IS "medium":
            EDIT _graphicsQuality = "high"
            fe3d:text_set_content("menu_graphics", "GFX:high")
            fe3d:text_set_size("menu_graphics", 0.35, 0.075)
        ELSE:
            EDIT _graphicsQuality = "low"
            fe3d:text_set_content("menu_graphics", "GFX:low")
            fe3d:text_set_size("menu_graphics", 0.30625, 0.075)
    ELIF _hoveredMenuButtonID IS "menu_sound":
        IF _isSoundEnabled IS <true>:
            EDIT _isSoundEnabled = <false>
            fe3d:text_set_content("menu_sound", "Sound:OFF")
            fe3d:text_set_size("menu_sound", 0.39375, 0.075)
        ELSE:
            EDIT _isSoundEnabled = <true>
            fe3d:text_set_content("menu_sound", "Sound:ON")
            fe3d:text_set_size("menu_sound", 0.35, 0.075)
    ELIF _hoveredMenuButtonID IS "menu_music":
        IF _isMusicEnabled IS <true>:
            EDIT _isMusicEnabled = <false>
            fe3d:text_set_content("menu_music", "Music:OFF")
            fe3d:text_set_size("menu_music", 0.39375, 0.075)
        ELSE:
            EDIT _isMusicEnabled = <true>
            fe3d:text_set_content("menu_music", "Music:ON")
            fe3d:text_set_size("menu_music", 0.35, 0.075)
    ELIF _hoveredMenuButtonID IS "menu_quit":
        fe3d:game_stop()

/// Apply settings
fe3d:sound_set_enabled(_isSoundEnabled)
fe3d:music_set_enabled(_isMusicEnabled)

/// Play button click sound
IF lmbPressed IS <true> AND _hoveredMenuButtonID NOT "":
    fe3d:sound_play_forced("button", 0, 0)
