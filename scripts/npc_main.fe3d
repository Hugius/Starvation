16 25
META script_type_update
META script_execution_waiting

/// NPC rotation
VEC3 camPos = fe3d:camera_get_position()
VEC3 npcPos = fe3d:model_get_position("npc")
VEC3 direction = camPos
DECR direction npcPos
DEC npcRot = math:atan2(direction.x, direction.z)
fe3d:model_set_rotation("npc", 0.0, npcRot, 0.0)

/// NPC hover
fe3d:text_set_visible("talk", <false>)
STR hoveredID = fe3d:raycast_into_model("npc", "", <true>)
IF hoveredID NOT "":
    DEC distance = math:distance(camPos, npcPos)
    IF distance LESS _useReach AND _isTalking IS <false>:
        fe3d:text_set_visible("talk", <true>)
    ELSE:
        EDIT hoveredID = ""

/// NPC dialogue - start & stop
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")
IF hoveredID NOT "" AND ePressed IS <true> AND _isTalking IS <false>:
    EDIT _isTalking = <true>
    fe3d:cursor_show()
ELIF ePressed IS <true> AND _isTalking IS <true>:
    CAST _dialogueIndex STR
    STR textID = misc:concat_strings("npc", _dialogueIndex)
    CAST _dialogueIndex INT
    fe3d:text_set_visible(textID, <false>)
    EDIT _isTalking = <false>
    EDIT _dialogueIndex = 0
    fe3d:cursor_hide()

/// NPC dialogue - process
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
IF _isTalking IS <true>:
    CAST _dialogueIndex STR
    STR textID = misc:concat_strings("npc", _dialogueIndex)
    CAST _dialogueIndex INT
    BOOL existing = fe3d:text_is_existing(textID)
    /// Check if reached end of dialogue
    IF existing IS <true>:
        fe3d:text_set_visible(textID, <true>)
        IF lmbPressed IS <true>:
            fe3d:text_set_visible(textID, <false>)
            INCR _dialogueIndex 1
    ELSE:
        EDIT _isTalking = <false>
        EDIT _dialogueIndex = 0
        fe3d:cursor_hide()
