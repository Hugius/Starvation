7 38
META script_type_update
META script_execution_waiting

/// Values
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update bread baking
fe3d:text_set_visible("bake", <false>)
STR hoveredID = fe3d:raycast_into_model("oven", "hole", <true>)
IF hoveredID NOT "" AND _isBuilding IS <false>:
    STR ovenAabbID = misc:concat_strings(hoveredID, "@main")
    VEC3 ovenSize = fe3d:aabb_get_size(ovenAabbID)
    DEC ovenHeight = fe3d:model_get_max_height(hoveredID)
    DEC ovenDistance = fe3d:raycast_into_model_distance("oven", "hole", <true>)
    IF ovenDistance IS -1.0:
        EDIT ovenDistance = 1000.0
    /// Check if oven is fully built & hovered
    BOOL isBuilt = (ovenHeight IS ovenSize.y OR ovenHeight MORE ovenSize.y)
    IF ovenDistance LESS _useReach AND isBuilt IS <true>:
        STR breadID = misc:concat_strings("bread:", hoveredID)
        BOOL isBreadExisting = fe3d:model_is_existing(breadID)
        /// Check if player has enough flour to bake
        IF isBreadExisting IS <false> AND _bag[5] MORE 0:
            fe3d:text_set_visible("bake", <true>)
            IF ePressed IS <true>:
                DECR _bag[5] _breadCount
                fe3d:sound_play_forced("bake", 0, 0)
                VEC3 pos = fe3d:model_get_position(hoveredID)
                VEC3 rot = fe3d:model_get_rotation(hoveredID)
                IF rot.y IS 0.0:
                    INCR pos.x 1.9
                ELIF rot.y IS 90.0:
                    DECR pos.z 1.9
                ELIF rot.y IS 180.0:
                    DECR pos.x 1.9
                ELSE:
                    INCR pos.z 1.9
                INCR pos.y 2.0
                INCR rot.y 90.0
                fe3d:model_place(breadID, "bread", pos.x, pos.y, pos.z)
                fe3d:model_set_rotation(breadID, rot.x, rot.y, rot.z)

/// Update bread pickup
EDIT hoveredID = fe3d:raycast_into_model("bread:oven", "", <true>)
IF hoveredID NOT "" AND _wasPickupPlaying IS <true> AND _isPickupPlaying IS <false>:
    DEC breadDistance = fe3d:raycast_into_model_distance("bread:oven", "", <true>)
    IF breadDistance IS -1.0:
        EDIT breadDistance = 1000.0
    IF breadDistance LESS _pickupReach:
        INCR _bag[6] _breadCount
        fe3d:model_delete(hoveredID)
        fe3d:sound_play_forced("bag", 0, 0)
