79 17
META script_type_update
META script_execution_waiting

/// Opening or closing building menu
EDIT _wasBuilding = _isBuilding
BOOL cPressed = fe3d:input_is_key_pressed("KEY_C")
IF cPressed IS <true> AND _isAbleToMove IS <true>:
    IF _isBuilding IS <false>:
        EDIT _isBuilding = <true>
    ELSE:
        EDIT _isBuilding = <false>

/// Update HUD visibility
IF _wasBuilding NOT _isBuilding OR _survivalTime IS 0:
    fe3d:image_set_visible("scroll_build", _isBuilding)
    fe3d:image_set_visible("wood_price_build", _isBuilding)
    fe3d:image_set_visible("stone_price_build", _isBuilding)
    fe3d:image_set_visible("clay_price_build", _isBuilding)
    fe3d:image_set_visible("iron_price_build", _isBuilding)
    fe3d:text_set_visible("wood_price_build", _isBuilding)
    fe3d:text_set_visible("stone_price_build", _isBuilding)
    fe3d:text_set_visible("clay_price_build", _isBuilding)
    fe3d:text_set_visible("iron_price_build", _isBuilding)

/// Update current building name & prices
IF _isBuilding IS <true>:
    fe3d:text_set_visible(_buildingTypes[_buildIndex], _isBuilding)
    INT woodPrice = _buildingTypesWoodPrice[_buildIndex]
    INT stonePrice = _buildingTypesStonePrice[_buildIndex]
    INT clayPrice = _buildingTypesClayPrice[_buildIndex]
    INT ironPrice = _buildingTypesIronPrice[_buildIndex]
    MUL woodPrice 10
    MUL stonePrice 10
    MUL clayPrice 10
    MUL ironPrice 10
    fe3d:text_set_size("wood_price_build", 0.025, 0.05)
    fe3d:text_set_size("stone_price_build", 0.025, 0.05)
    fe3d:text_set_size("clay_price_build", 0.025, 0.05)
    fe3d:text_set_size("iron_price_build", 0.025, 0.05)
    IF woodPrice MORE 9:
        fe3d:text_set_size("wood_price_build", 0.05, 0.05)
    IF stonePrice MORE 9:
        fe3d:text_set_size("stone_price_build", 0.05, 0.05)
    IF clayPrice MORE 9:
        fe3d:text_set_size("clay_price_build", 0.05, 0.05)
    IF ironPrice MORE 9:
        fe3d:text_set_size("iron_price_build", 0.05, 0.05)
    CAST woodPrice STR
    CAST stonePrice STR
    CAST clayPrice STR
    CAST ironPrice STR
    fe3d:text_set_content("wood_price_build", woodPrice)
    fe3d:text_set_content("stone_price_build", stonePrice)
    fe3d:text_set_content("clay_price_build", clayPrice)
    fe3d:text_set_content("iron_price_build", ironPrice)
    INT wp = _buildingTypesWoodPrice[_buildIndex]
    INT sp = _buildingTypesStonePrice[_buildIndex]
    INT cp = _buildingTypesClayPrice[_buildIndex]
    INT ip = _buildingTypesIronPrice[_buildIndex]
    DECR wp 1
    DECR sp 1
    DECR cp 1
    DECR ip 1
    BOOL w = (_bag[0] MORE wp)
    BOOL s = (_bag[1] MORE sp)
    BOOL c = (_bag[2] MORE cp)
    BOOL i = (_bag[10] MORE ip)
    BOOL can = (w IS <true> AND s IS <true> AND c IS <true> AND i IS <true>)
    EDIT _canAffordBuilding = can

/// Check if a built building is hovered
STR toolID = _inventory[_equipIndex]
STR raycastID = ""
INT length = 0
EDIT _hoveredBuildingID = ""
EDIT _hoveredBuildingDistance = 0.0
IF toolID IS "hammer_stone" OR toolID IS "hammer_iron":
    STR hoveredID = ""
    DEC distance = 0.0
    INT index = 0
    INT maxIndex = misc:get_list_size("_buildingTypes")
    LOOP:
        IF index IS maxIndex:
            BREAK
        /// Remove "_display" from ID to extract building name
        EDIT raycastID = _buildingTypes[index]
        EDIT length = misc:get_string_size(raycastID)
        DECR length 8
        EDIT raycastID = misc:get_string_part(raycastID, 0, length)
        EDIT distance = fe3d:raycast_into_model_distance(raycastID, "", <true>)
        IF distance IS -1.0:
            EDIT distance = 1000.0
        ELSE:
            EDIT hoveredID = fe3d:raycast_into_model(raycastID, "", <true>)
            IF hoveredID NOT "" AND _isInvOpen IS <true>:
                EDIT _hoveredBuildingID = hoveredID
                EDIT _hoveredBuildingDistance = distance
                BREAK
        INCR index 1
