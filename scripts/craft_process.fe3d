5 14
META script_type_update
META script_execution_waiting

EDIT _wasCrafting = _isCrafting
IF _isCrafting IS <true>:
    /// Values
    INT scrollDir = fe3d:input_get_mousewheel_direction()
    BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
    STR confirm = fe3d:raycast_into_billboard("craft_confirm", <true>)
    STR cancel = fe3d:raycast_into_billboard("craft_cancel", <true>)
    BOOL confirmed = (confirm NOT "")
    BOOL cancelled = (cancel NOT "")
    /// Check if player has the required resources
    IF confirmed IS <true>:
        INT wp = _toolTypesWoodPrice[_craftIndex]
        INT sp = _toolTypesStonePrice[_craftIndex]
        INT ip = _toolTypesIronPrice[_craftIndex]
        DECR wp 1
        DECR sp 1
        DECR ip 1
        EDIT confirmed = (_bag[0] MORE wp AND _bag[1] MORE sp AND _bag[10] MORE ip)
    /// Confirm button hovering
    IF confirmed IS <true>:
        fe3d:billboard_set_size("craft_confirm", 1.25, 1.25)
        fe3d:billboard_set_color("craft_confirm", 1.0, 1.0, 1.0)
    ELSE:
        fe3d:billboard_set_size("craft_confirm", 1.0, 1.0)
        fe3d:billboard_set_color("craft_confirm", 0.0, 0.0, 0.0)
    /// Cancel button hovering
    IF cancelled IS <true>:
        fe3d:billboard_set_size("craft_cancel", 1.25, 1.25)
        fe3d:billboard_set_color("craft_cancel", 1.0, 0.1, 0.1)
    ELSE:
        fe3d:billboard_set_size("craft_cancel", 1.0, 1.0)
        fe3d:billboard_set_color("craft_cancel", 0.2, 0.0, 0.0)
    /// Update crafting menu interaction
    IF confirmed IS <true> AND lmbPressed IS <true>:
        /// Hide old tool
        IF _inventory[_equipIndex] NOT "":
            fe3d:model_set_visible(_inventory[_equipIndex], <false>)
        /// Remove "_display" from ID to extract item name
        STR itemName = _toolTypes[_craftIndex]
        INT length = misc:get_string_size(itemName)
        DECR length 8
        EDIT itemName = misc:get_string_part(itemName, 0, length)
        /// Equip tool
        EDIT _equipIndex = _toolTypesIndex[_craftIndex]
        EDIT _inventory[_equipIndex] = itemName
        /// Pay the price
        DECR _bag[0] _toolTypesWoodPrice[_craftIndex]
        DECR _bag[1] _toolTypesStonePrice[_craftIndex]
        DECR _bag[10] _toolTypesIronPrice[_craftIndex]
        /// Determine tool durability
        INT i = _craftIndex
        IF i IS 1 OR i IS 3 OR i IS 5 OR i IS 7 OR i IS 8 OR i IS 9 OR i IS 11:
            /// Iron lasts longer
            EDIT _durabilities[_equipIndex] = 10.0
        ELSE:
            EDIT _durabilities[_equipIndex] = 1.0
        fe3d:sound_play_forced("craft", 0, 0)
    ELIF cancelled IS <true> AND lmbPressed IS <true>:
        EDIT _isCrafting = <false>
        fe3d:cursor_hide()
    ELIF scrollDir NOT 0:
        /// Hide old tool + text
        fe3d:model_set_visible(_toolTypes[_craftIndex], <false>)
        fe3d:text_set_visible(_toolTypes[_craftIndex], <false>)
        INT maxIndex = misc:get_list_size("_toolTypes")
        DECR maxIndex 1
        /// Scroll to next tool
        IF _craftIndex IS maxIndex AND scrollDir IS 1:
            EDIT _craftIndex = 0
        ELIF _craftIndex IS 0 AND scrollDir IS -1:
            EDIT _craftIndex = maxIndex
        ELSE:
            INCR _craftIndex scrollDir
        /// Show new tool + text
        fe3d:model_set_visible(_toolTypes[_craftIndex], <true>)
        fe3d:text_set_visible(_toolTypes[_craftIndex], <true>)
