41 28
META script_type_update
META script_execution_waiting

/// Values
STR toolID = _inventory[_equipIndex]
STR hoveredID = _hoveredBuildingID
DEC distance = _hoveredBuildingDistance

/// Update building process
fe3d:text_set_visible("build", <false>)
IF hoveredID NOT "":
    DEC height = fe3d:model_get_max_height(hoveredID)
    STR idPart = misc:get_string_part(hoveredID, 0, 3)
    IF idPart IS "sma":
        INT hoveredIndex = 0
        VEC3 size = fe3d:aabb_get_size("small_house_display@back")
    ELIF idPart IS "med":
        INT hoveredIndex = 1
        VEC3 size = fe3d:aabb_get_size("medium_house_display@back")
    ELIF idPart IS "big":
        INT hoveredIndex = 2
        VEC3 size = fe3d:aabb_get_size("big_house_display@back")
    ELIF idPart IS "sto":
        INT hoveredIndex = 3
        VEC3 size = fe3d:aabb_get_size("storage_display@main")
    ELIF idPart IS "wel":
        INT hoveredIndex = 4
        VEC3 size = fe3d:aabb_get_size("well_display@well")
    ELIF idPart IS "mil":
        INT hoveredIndex = 5
        VEC3 size = fe3d:aabb_get_size("mill_display@main")
    ELIF idPart IS "ove":
        INT hoveredIndex = 6
        VEC3 size = fe3d:aabb_get_size("oven_display@main")
    ELIF idPart IS "cam":
        INT hoveredIndex = 7
        VEC3 size = fe3d:aabb_get_size("campfire_display@logs")
    ELIF idPart IS "tab":
        INT hoveredIndex = 8
        VEC3 size = fe3d:aabb_get_size("table_display@table")
    ELIF idPart IS "sme":
        INT hoveredIndex = 9
        VEC3 size = fe3d:aabb_get_size("smelter_display@main")
    INT wp = _buildingTypesWoodPrice[hoveredIndex]
    INT sp = _buildingTypesStonePrice[hoveredIndex]
    INT cp = _buildingTypesClayPrice[hoveredIndex]
    INT ip = _buildingTypesIronPrice[hoveredIndex]
    DECR wp 1
    DECR sp 1
    DECR cp 1
    DECR ip 1
    BOOL afford1 = (_bag[0] MORE wp AND _bag[1] MORE sp)
    BOOL afford2 = (_bag[2] MORE cp AND _bag[9] MORE ip)
    BOOL canAfford = (afford1 IS <true> AND afford2 IS <true>)
    IF distance LESS _toolReach AND height LESS size.y AND canAfford IS <true>:
        fe3d:text_set_visible("build", <true>)
        IF _wasHammerPlaying IS <true> AND _isHammerPlaying IS <false>:
            DEC heightIncrease = size.y
            MUL heightIncrease 0.10001
            INCR height heightIncrease
            fe3d:model_set_max_height(hoveredID, height)
            DECR _bag[0] _buildingTypesWoodPrice[hoveredIndex]
            DECR _bag[1] _buildingTypesStonePrice[hoveredIndex]
            DECR _bag[2] _buildingTypesClayPrice[hoveredIndex]
            DECR _bag[10] _buildingTypesIronPrice[hoveredIndex]
            DECR _durabilities[6] _durabilityHit
            BOOL isBuilt = (height IS size.y OR height MORE size.y)
            IF isBuilt IS <true>:
                EDIT _finishedBuildingID = hoveredID

/// Update hammer sound
BOOL isSoundPlaying = fe3d:sound_is_playing("hammer")
BOOL isAllowed = fe3d:text_is_visible("build")
IF isAllowed IS <true> AND _isHammerPlaying IS <true> AND isSoundPlaying IS <false>:
    INT frameIndex = fe3d:model_get_animation_frame_index(toolID, "hammer_build")
    IF frameIndex IS 1:
        fe3d:sound_play("hammer", 0, 0)
