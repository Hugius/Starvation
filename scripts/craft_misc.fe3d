21 7
META script_type_update
META script_execution_waiting

/// Values
STR toolID = _inventory[_equipIndex]
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update workbench hover
STR hoveredID = fe3d:raycast_into_model("workbench", "", <true>)
DEC distance = 0.0
IF hoveredID NOT "":
    EDIT distance = fe3d:raycast_into_model_distance("workbench", "", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0

/// Update crafting menu open & close
fe3d:text_set_visible("craft", <false>)
IF ePressed IS <true> AND _isCrafting IS <true>:
    EDIT _isCrafting = <false>
    fe3d:cursor_hide()
ELIF hoveredID NOT "" AND distance LESS _useReach AND _isCrafting IS <false>:
    IF _isToolPlaying IS <false> AND _isBuilding IS <false>:
        fe3d:text_set_visible("craft", <true>)
        IF ePressed IS <true>:
            EDIT _isCrafting = <true>
            fe3d:cursor_show()

/// Update HUD visibility
IF _wasCrafting NOT _isCrafting OR _survivalTime IS 0:
    fe3d:model_set_visible(_toolTypes[_craftIndex], _isCrafting)
    fe3d:image_set_visible("scroll_craft", _isCrafting)
    fe3d:image_set_visible("wood_price_craft", _isCrafting)
    fe3d:image_set_visible("stone_price_craft", _isCrafting)
    fe3d:image_set_visible("iron_price_craft", _isCrafting)
    fe3d:text_set_visible("wood_price_craft", _isCrafting)
    fe3d:text_set_visible("stone_price_craft", _isCrafting)
    fe3d:text_set_visible("iron_price_craft", _isCrafting)
    fe3d:billboard_set_visible("craft_confirm", _isCrafting)
    fe3d:billboard_set_visible("craft_cancel", _isCrafting)
    fe3d:text_set_visible(_toolTypes[_craftIndex], _isCrafting)

/// Update price tag
IF _isCrafting IS <true>:
    INT woodPrice = _toolTypesWoodPrice[_craftIndex]
    INT stonePrice = _toolTypesStonePrice[_craftIndex]
    INT ironPrice = _toolTypesIronPrice[_craftIndex]
    CAST woodPrice STR
    CAST stonePrice STR
    CAST ironPrice STR
    fe3d:text_set_content("wood_price_craft", woodPrice)
    fe3d:text_set_content("stone_price_craft", stonePrice)
    fe3d:text_set_content("iron_price_craft", ironPrice)
