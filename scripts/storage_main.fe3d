87 54
META script_type_update
META script_execution_waiting

/// Values
DEC cursorX = fe3d:cursor_get_position_x()
DEC cursorY = fe3d:cursor_get_position_y()
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")
BOOL ePressed = fe3d:input_is_key_pressed("KEY_E")

/// Update storage hover
EDIT _wasInStorage = _isInStorage
STR hoveredID = fe3d:raycast_into_model("storage0", "door", <true>)
DEC distance = 0.0
IF hoveredID NOT "":
    EDIT distance = fe3d:raycast_into_model_distance("storage0", "door", <true>)
    IF distance IS -1.0:
        EDIT distance = 1000.0

/// Update storage menu open & close
fe3d:text_set_visible("storage", <false>)
IF ePressed IS <true> AND _isInStorage IS <true>:
    EDIT _isInStorage = <false>
    fe3d:cursor_hide()
ELIF hoveredID NOT "" AND distance LESS _useReach AND _isInStorage IS <false>:
    DEC height = fe3d:model_get_max_height(hoveredID)
    VEC3 size = fe3d:aabb_get_size("storage_display@main")
    BOOL isBuilt = (height IS size.y OR height MORE size.y)
    IF isBuilt IS <true> AND _isToolPlaying IS <false> AND _isBuilding IS <false>:
        fe3d:text_set_visible("storage", <true>)
        IF ePressed IS <true>:
            EDIT _isInStorage = <true>
            fe3d:cursor_show()

/// Update storage menu buttons hover
IF _isInStorage IS <true>:
    STR buttonID = ""
    VEC3 color = _storageButtonColor
    DEC buttonWidth = 0.0
    DEC buttonHeight = 0.0
    DEC buttonX = 0.0
    DEC buttonY = 0.0
    DEC xDifference = 0.0
    DEC yDifference = 0.0
    INT index = 0
    INT maxIndex = misc:get_list_size("_storageButtonIDs")
    BOOL isButtonHovered = <false>
    LOOP:
        IF index IS maxIndex:
            BREAK
        ELSE:
            EDIT buttonID = _storageButtonIDs[index]
            EDIT buttonWidth = fe3d:image_get_width(buttonID)
            MUL buttonWidth 0.5
            EDIT buttonHeight = fe3d:image_get_height(buttonID)
            MUL buttonHeight 0.5
            EDIT buttonX = fe3d:image_get_position_x(buttonID)
            EDIT buttonY = fe3d:image_get_position_y(buttonID)
            EDIT xDifference = buttonX
            DECR xDifference cursorX
            EDIT xDifference = math:abs(xDifference)
            EDIT yDifference = buttonY
            DECR yDifference cursorY
            EDIT yDifference = math:abs(yDifference)
            IF xDifference LESS buttonWidth AND yDifference LESS buttonHeight:
                fe3d:image_set_color(buttonID, color.r, color.g, color.b)
                EDIT isButtonHovered = <true>
                BREAK
            ELSE:
                fe3d:image_set_color(buttonID, 1.0, 1.0, 1.0)
            INCR index 1
    /// Update storage menu buttons click
    IF isButtonHovered IS <true> AND lmbPressed IS <true>:
        BOOL isEven = math:is_even(index)
        /// Even = plus, not even = minus
        IF isEven IS <true>:
            DIV index 2
            IF _bag[index] MORE 0:
                DECR _bag[index] 1
                INCR _storage[index] 1
                fe3d:sound_play_forced("button", 0, 0)
                fe3d:sound_play_forced("bag", 0, 0)
        ELSE:
            DECR index 1
            DIV index 2
            IF _storage[index] MORE 0:
                DECR _storage[index] 1
                INCR _bag[index] 1
                fe3d:sound_play_forced("button", 0, 0)
                fe3d:sound_play_forced("bag", 0, 0)
