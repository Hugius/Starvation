68 27
META script_type_update
META script_execution_waiting

/// Values
BOOL up = fe3d:input_is_key_down("KEY_W")
BOOL down = fe3d:input_is_key_down("KEY_S")
BOOL left = fe3d:input_is_key_down("KEY_A")
BOOL right = fe3d:input_is_key_down("KEY_D")
BOOL shift = fe3d:input_is_key_down("KEY_LSHIFT")
EDIT _isMoving = (up IS <true> OR down IS <true> OR left IS <true> OR right IS <true>)
DEC accSpeed = _playerAccelerationSpeed
DEC minSpeed = _maxPlayerSpeed
NEG minSpeed
DEC maxSpeed = _maxPlayerSpeed

/// Movement must be slowed when carrying too much
IF _totalBagWeight MORE _maxBagWeight:
    DEC weightFactor = _maxBagWeight
    DIV weightFactor _totalBagWeight
    MUL minSpeed weightFactor
    MUL maxSpeed weightFactor

/// Movement must be slowed if underwater
IF _isUnderwater IS <true>:
    MUL minSpeed 0.5
    MUL maxSpeed 0.5

/// Determine max movement speed
IF shift IS <true> AND up IS <true>:
    IF _stamina MORE 0.0:
        MUL minSpeed 2.0
        MUL maxSpeed 2.0
        EDIT _isRunning = <true>
        DECR _stamina _staminaRunFactor
    ELSE:
        EDIT _isRunning = <false>
ELSE:
    EDIT _isRunning = <false>
    IF _stamina LESS 1.0:
        INCR _stamina _staminaWalkFactor

/// Update camera movement
IF _isAbleToMove IS <true>:
    IF left IS <true> AND _movingAcc.x MORE minSpeed:
        DECR _movingAcc.x accSpeed
    IF right IS <true> AND _movingAcc.x LESS maxSpeed:
        INCR _movingAcc.x accSpeed
    IF up IS <true> AND _movingAcc.z LESS maxSpeed:
        INCR _movingAcc.z accSpeed
    IF down IS <true> AND _movingAcc.z MORE minSpeed:
        DECR _movingAcc.z accSpeed
    IF left IS <false> AND _movingAcc.x LESS 0.0:
        EDIT _movingAcc.x = 0.0
    IF right IS <false> AND _movingAcc.x MORE 0.0:
        EDIT _movingAcc.x = 0.0
    IF up IS <false> AND _movingAcc.z MORE 0.0:
        EDIT _movingAcc.z = 0.0
    IF down IS <false> AND _movingAcc.z LESS 0.0:
        EDIT _movingAcc.z = 0.0
    INCR _movingSpeed _movingAcc
    MUL _movingAcc 0.99
    MUL _movingSpeed 0.9
    fe3d:camera_follow_x(_movingSpeed.x)
    fe3d:camera_follow_z(_movingSpeed.z)
    VEC3 camPos = fe3d:camera_get_position()
    IF camPos.x MORE 150.0:
        EDIT camPos.x = 150.0
    IF camPos.x LESS -130.0:
        EDIT camPos.x = -130.0
    IF camPos.z MORE 160.0:
        EDIT camPos.z = 160.0
    IF camPos.z LESS -120.0:
        EDIT camPos.z = -120.0
    fe3d:camera_set_position(camPos.x, camPos.y, camPos.z)
ELSE:
    MUL _movingAcc 0.99
    MUL _movingSpeed 0.9

/// Update movement sounds
BOOL isWalkStarted = fe3d:sound_is_started("walk")
BOOL isRunStarted = fe3d:sound_is_started("run")
BOOL isWalkPlaying = fe3d:sound_is_playing("walk")
BOOL isRunPlaying = fe3d:sound_is_playing("run")
BOOL isWalkPaused = fe3d:sound_is_paused("walk")
BOOL isRunPaused = fe3d:sound_is_paused("run")
BOOL ableToPlay = (_isUnderwater IS <false> AND _isJumping IS <false>)
IF _isMoving IS <true> AND ableToPlay IS <true> AND _isRunning IS <false>:
    IF isWalkStarted IS <false>:
        fe3d:sound_play("walk", -1, 250)
ELSE:
    IF isWalkStarted IS <true>:
        fe3d:sound_stop("walk", 250)
IF _isRunning IS <true> AND ableToPlay IS <true>:
    IF isRunStarted IS <false>:
        fe3d:sound_play("run", -1, 250)
ELSE:
    IF isRunStarted IS <true>:
        fe3d:sound_stop("run", 250)
