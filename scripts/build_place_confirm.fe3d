43 10
META script_type_update
META script_execution_waiting

/// Values
BOOL lmbPressed = fe3d:input_is_mouse_pressed("BUTTON_LEFT")

/// Update building placement
IF _isBuilding IS <true> AND lmbPressed IS <true>:
    IF _isBuildPlaceValid IS <true>:
        IF _buildIndex IS 0:
            VEC3 size = fe3d:aabb_get_size("small_house_display@back")
            STR previewID = "house1"
        ELIF _buildIndex IS 1:
            VEC3 size = fe3d:aabb_get_size("medium_house_display@back")
            STR previewID = "house2"
        ELIF _buildIndex IS 2:
            VEC3 size = fe3d:aabb_get_size("big_house_display@back")
            STR previewID = "house3"
        ELIF _buildIndex IS 3:
            VEC3 size = fe3d:aabb_get_size("storage_display@main")
            STR previewID = "storage"
        ELIF _buildIndex IS 4:
            VEC3 size = fe3d:aabb_get_size("well_display@well")
            STR previewID = "well"
        ELIF _buildIndex IS 5:
            VEC3 size = fe3d:aabb_get_size("mill_display@main")
            STR previewID = "mill"
        ELIF _buildIndex IS 6:
            VEC3 size = fe3d:aabb_get_size("oven_display@main")
            STR previewID = "oven"
        ELIF _buildIndex IS 7:
            VEC3 size = fe3d:aabb_get_size("campfire_display@logs")
            STR previewID = "campfire"
        ELIF _buildIndex IS 8:
            VEC3 size = fe3d:aabb_get_size("table_display@table")
            STR previewID = "table"
        ELIF _buildIndex IS 9:
            VEC3 size = fe3d:aabb_get_size("smelter_display@main")
            STR previewID = "smelter"
        INT counter = _buildCounters[_buildIndex]
        CAST counter STR
        /// Remove "_display" from ID to extract item name
        STR newID = _buildingTypes[_buildIndex]
        INT length = misc:get_string_size(newID)
        DECR length 8
        EDIT newID = misc:get_string_part(newID, 0, length)
        EDIT newID = misc:concat_strings(newID, counter)
        INCR _buildCounters[_buildIndex] 1
        VEC3 buildPos = _buildPosition
        fe3d:model_place(newID, previewID, buildPos.x, buildPos.y, buildPos.z)
        fe3d:model_set_rotation(newID, 0.0, _buildRotation, 0.0)
        DEC height = size.y
        MUL height 0.1
        fe3d:model_set_max_height(newID, height)
        DECR _bag[0] _buildingTypesWoodPrice[_buildIndex]
        DECR _bag[1] _buildingTypesStonePrice[_buildIndex]
        DECR _bag[2] _buildingTypesClayPrice[_buildIndex]
        DECR _bag[8] _buildingTypesIronPrice[_buildIndex]
        DECR _durabilities[6] _durabilityHit
        fe3d:sound_play("hammer", 0, 0)
